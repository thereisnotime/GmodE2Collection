@name -Fast- Dump LogMem
@inputs Log:wirelink Dump
@outputs Reading
@persist I Str:string Lim
@trigger Dump

if(Dump&~Dump) {
    Str = ""
    Lim = Log[0]
    print("Reading out ", Lim, " bytes:")
    I = 1
    Reading = 1
    timer("read", 500)
}

if(clk("read")) {
    timer("read", 500)
    local Val = Log[I]
    while(I < Lim) {
        if(Val == toByte("\n")) {
            print(Str)
            Str = ""
            I = I + 1
            exit()
        } else {
            Str = Str + toChar(Val)
        }
        I = I + 1
        if(!perf()) {
            exit()
        }
        Val = Log[I]
    }
    print("[REMAINING:] ", Str)
    print("Syslog dump ended, ", I, " bytes read.")
    stoptimer("read")
    Reading = 0
}
