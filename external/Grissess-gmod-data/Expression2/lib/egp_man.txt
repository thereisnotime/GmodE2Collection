#EGP Basic Management

function table newEGPManager(WL:wirelink) {
    local Ret=table()
    Ret["WL", wirelink]=WL
    Ret["Allocated", table]=table()
    for(I=1,egpMaxObjects()) {
        Ret["Allocated", table]:pushNumber(egpMaxObjects()-I+1)
    }
    Ret["Size", vector2]=vec2(512, 512)
    return Ret
}

function wirelink table:emEGP() {
    return This["WL", wirelink]
}

function vector2 table:emSize() {
    return This["Size", vector2]
}

function vector2 table:emSizeFromPlayer(Player:entity) {
    This["Size", vector2]=egpScrSize(Player)
    return This["Size", vector2]
}

function vector2 table:emDefaultSize() {
    This["Size", vector2]=vec2(512, 512)
    return This["Size", vector2]
}

function number table:emAlloc() {
    return This["Allocated", table]:popNumber()
}

function void table:emFree(Index:number) {
    This:emEGP():egpRemove(Index)
    This["Allocated", table]:pushNumber(Index)
}

function void table:emClearAllocations() {
    This["Allocated", table]:clear()
    for(I=1,egpMaxObjects()) {
        This["Allocated", table]:pushNumber(egpMaxObjects()-I+1)
    }
}

function void table:emClear() {
    This:emEGP():egpClear()
    This:emClearAllocations()
}
