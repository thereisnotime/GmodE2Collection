@name Doctor autoshop
@inputs LastMoneyPotAmount MoneyPotUpdated ConsoleScreen:wirelink
@outputs ArmorUser MedkitUser
@persist ArmorPrice MedkitPrice ConsoleScreenFound
@trigger MoneyPotUpdated
@autoupdate


if (first()) {
    ArmorPrice = 200
    MedkitPrice = 140
    ConsoleScreenFound = 0
}

if (ConsoleScreenFound == 0) {
    interval(1000) #Keep firing loops until the console screen is found

    #Draw to the console screen when it is detected
    if (ConsoleScreen:inputs():count() != 0) {
        ConsoleScreenFound = 1
        print("Console screen found!")
        
        ConsoleScreen:writeCell(2041,1)
        ConsoleScreen:writeString("       Auto-Doctor store      ",0,0,999,400)
        ConsoleScreen:writeString("Insert money to buy:",0,2,999,0)
        ConsoleScreen:writeString("$" + ArmorPrice + ": Armor",0,3,999,0)
        ConsoleScreen:writeString("$" + MedkitPrice + ": Medkit",0,4,999,0)
        ConsoleScreen:writeString("Simply drop the money in the  chute to spawn items.",0,14,999,0)
        ConsoleScreen:writeString("                              ",0,17,999,400)
        
        exit() #Break the iteration if it was only needed for drawing the console screen
    }
}

if (clk("switchOffUsers")) {
    entity():soundPlay(0, 0, "Buttons.snd5") #Purchase successful SFX
    ArmorUser = 0
    MedkitUser = 0
    
} elseif (LastMoneyPotAmount >= ArmorPrice) {
    ArmorUser = 1
    timer("switchOffUsers", 500)
    
} elseif (LastMoneyPotAmount >= MedkitPrice) {
    MedkitUser = 1
    timer("switchOffUsers", 500)
    
} elseif (LastMoneyPotAmount != 0) {
    entity():soundPlay(0, 0, "Buttons.snd8") #Play error sound effect when someone adds money that isn't enough to buy anything
}
