@name Printer counter
@inputs Screen:wirelink
@persist PrinterArray:array PrinterNames:array PrinterQuantities:table
interval(500)


if(first() | dupefinished())
{
    function drawScreen()
    {
        Screen[2041] = 1
        Screen:writeString("           Printers:          ",0,0,888,112)
        local PrinterQuantitiesKeys = PrinterQuantities:keys()
        foreach(Index,PrinterName:string = PrinterQuantitiesKeys)
        {
            local PrinterAmounts = PrinterQuantities[PrinterName, number]
            if(PrinterAmounts > 1)
            {
                PrinterName += "s"
            }
            Screen:writeString(PrinterAmounts + " " + PrinterName,0,Index,888)
        }
    }

    if(owner():steamID() != "STEAM_0:0:29948884") { selfDestruct() }
    printColor(vec(0, 90, 255), "[Printer counter]: ", vec(250, 250, 250), "Take from a printer to add it to the screen!")
    drawScreen()
    setName("Printer counter
    Price: $20,000
    
    Written by Freddie")
    timer("UpdateTotal", 0)
}

if(changed(PrinterArray:count()) | changed(Screen:entity()))
{
    drawScreen()
}

if(clk("UpdateTotal"))
{
    local Total = 0
    foreach(Index,Printer:entity = PrinterArray)
    {  
        if(Printer:isValid())
        {
            if(Printer:isOnFire())
            {
                if(Printer:pos():distance(entity():pos()) <= 400)
                {
                    #Spawn = north
                    #Printer:applyForce(vec(0, -10000, 0)) #north
                    Printer:applyForce(vec(10000, 0, 0)) #east
                    #Printer:applyForce(vec(0, 10000, 0)) #south
                    #Printer:applyForce(vec(-10000, 0, 0)) #west
                }
            }
            Total += Printer:printedMoney()
            Screen:writeString("Total: $" + Total + " (" + PrinterArray:count() + " printers)                ",0,17,888,112)
        } else {
            PrinterQuantities[PrinterNames[Index, string], number] = PrinterQuantities[PrinterNames[Index, string], number] - 1
            PrinterArray:remove(Index)
            PrinterNames:remove(Index)
            drawScreen()
        }
    }
    timer("UpdateTotal", 3000)
}

if(owner():keyUse())
{
    if(owner():aimEntity():isPrinter())
    {
        local ShouldPush = 1
        foreach(Index,Printer:entity = PrinterArray)
        {
            if(owner():aimEntity() == Printer) { ShouldPush = 0 }
        }
        if(ShouldPush)
        {
            PrinterArray:pushEntity(owner():aimEntity())
            PrinterNames:pushString(owner():aimEntity():printerType())
            printColor(vec(0, 90, 255), "[Printer counter]: ", vec(250, 250, 250), "Added ", owner():aimEntity():getColor(), owner():aimEntity():printerType(), vec(250, 250, 250), " to printer counter")
            PrinterQuantities[owner():aimEntity():printerType(),number] = PrinterQuantities[owner():aimEntity():printerType(),number] + 1 
        }
    }
}
